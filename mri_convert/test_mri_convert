#!/bin/tcsh -f

#
# test_mri_convert
#
# execute mri_convert and check results against expected results
# (and test mri_make_uchar while we're at it)
#
# Original Author: Nick Schmansky
# CVS Revision Info:
#    $Author: nicks $
#    $Date: 2011/05/02 21:02:42 $
#    $Revision: 1.8 $
#
# Copyright Â© 2011 The General Hospital Corporation (Boston, MA) "MGH"
#
# Terms and conditions for use, reproduction, distribution and contribution
# are found in the 'FreeSurfer Software License Agreement' contained
# in the file 'LICENSE' found in the FreeSurfer distribution, and here:
#
# https://surfer.nmr.mgh.harvard.edu/fswiki/FreeSurferSoftwareLicense
#
# Reporting: freesurfer@nmr.mgh.harvard.edu
#
# General inquiries: freesurfer@nmr.mgh.harvard.edu
#

set VERSION='$Id: test_mri_convert,v 1.8 2011/05/02 21:02:42 nicks Exp $'

umask 002

#
# extract testing data
#
gunzip -c testdata.tar.gz | tar xvf -

#
# run mri_convert using typical input file formats
#

cd testdata

# --conform function
set cmd=(../mri_convert rawavg.mgz orig.mgz --conform)
echo ""
$cmd
if ($status != 0) then
  echo "mri_convert FAILED"
  exit 1
endif

# DICOM
set cmd=(../mri_convert 261000-10-60.dcm dicom.mgz)
echo ""
$cmd
if ($status != 0) then
  echo "mri_convert FAILED"
  exit 1
endif

# Nifti
set cmd=(../mri_convert nifti.nii nifti.mgz)
echo ""
$cmd
if ($status != 0) then
  echo "mri_convert FAILED"
  exit 1
endif

# Analyze
set cmd=(../mri_convert analyze.img analyze.mgz)
echo ""
$cmd
if ($status != 0) then
  echo "mri_convert FAILED"
  exit 1
endif



# mri_make_uchar test
# data (nu.mgz) is from ADNI subj 0030_m12, which was problematic because
# mri_nu_correct.mni float thing would mess with the histogramming stuff
# in mri_normalize.  mri_make_uchar was introduced to fix that, so this is
# a regression test of sorts.
set cmd=(../mri_make_uchar nu.mgz talairach.xfm nuuc.mgz)
echo ""
echo $cmd
$cmd
if ($status != 0) then
  echo "mri_make_uchar FAILED"
  exit 1
endif



echo ""
echo ""
echo ""

#
# compare expected results with actual (produced) results
#

set TEST_FILES=( orig.mgz dicom.mgz nifti.mgz analyze.mgz nuuc.mgz )
foreach tstvol ($TEST_FILES)
  # now compare expected with observed data
  set REF_VOL  = freesurfer.mgz
  if ( "x$tstvol" == "xorig.mgz" ) then
    set REF_VOL = ref_orig.mgz
    if ( "`uname -p`" == "powerpc" ) set REF_VOL = ref_powerpc_orig.mgz
  endif
  if ( "x$tstvol" == "xnuuc.mgz" ) then
    set REF_VOL = nuuc_ref.mgz
  endif
  set TST_VOL  = $tstvol
  set DIFF_VOL = $PWD/mri_diff-$tstvol
  set MRIDIFFF = $PWD/mri_diff-$tstvol.txt
  if ("x$tstvol" == "xdicom.mgz") then
    set ACQ=""
  else
    set ACQ="--notallow-acq"
  endif
  set cmd=(../../mri_diff/mri_diff --debug \
            --thresh 0.0 \
            --res-thresh 0.000001 \
            --geo-thresh 0.000008 \
            $ACQ --log $MRIDIFFF \
            $REF_VOL $TST_VOL --diff $DIFF_VOL);
  echo ""
  echo $cmd
  $cmd
  set diff_status=$status
  if ($diff_status != 0) then
    echo "$cmd FAILED (exit status=$diff_status)"
    exit 1
  endif
end

#
# cleanup
#
cd ..
rm -Rf testdata

echo ""
echo "test_mri_convert passed all tests"
exit 0
