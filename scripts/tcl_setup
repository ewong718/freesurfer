#! /bin/tcsh -ef

#
# tcl_setup
#
# Setup for apps using Tcl/Tk/Tix/BLT package included with Freesurfer
#
# Original Author: Nick Schmansky
# CVS Revision Info:
#    $Author: nicks $
#    $Date: 2011/10/05 17:17:10 $
#    $Revision: 1.7 $
#
# Copyright Â© 2011 The General Hospital Corporation (Boston, MA) "MGH"
#
# Terms and conditions for use, reproduction, distribution and contribution
# are found in the 'FreeSurfer Software License Agreement' contained
# in the file 'LICENSE' found in the FreeSurfer distribution, and here:
#
# https://surfer.nmr.mgh.harvard.edu/fswiki/FreeSurferSoftwareLicense
#
# Reporting: freesurfer@nmr.mgh.harvard.edu
#
#


### --  use NMR Center Tcl/Tk/Tix/BLT package -- ####

if ( $?FREESURFER_HOME ) then
  if ( -e $FREESURFER_HOME/lib/tcltktixblt ) then
    set FS_TCL_HOME=$FREESURFER_HOME/lib/tcltktixblt
  else if ( -e /usr/pubsw/packages/tcltktixblt/current ) then
    set FS_TCL_HOME=/usr/pubsw/packages/tcltktixblt/current
  endif
else if ( -e /usr/pubsw/packages/tcltktixblt/current ) then
  set FS_TCL_HOME=/usr/pubsw/packages/tcltktixblt/current
endif

if ( $?FS_TCL_HOME ) then

  if ( -e $FS_TCL_HOME/bin ) then
    setenv PATH "$FS_TCL_HOME/bin":"$PATH"
  endif

  if ( -e $FS_TCL_HOME/lib ) then
    setenv TCLLIBPATH $FS_TCL_HOME/lib
    if ( -e $TCLLIBPATH/tcl8.3 ) setenv TCL_LIBRARY $TCLLIBPATH/tcl8.3
    if ( -e $TCLLIBPATH/tcl8.4 ) setenv TCL_LIBRARY $TCLLIBPATH/tcl8.4
    if ( -e $TCLLIBPATH/tcl8.5 ) setenv TCL_LIBRARY $TCLLIBPATH/tcl8.5
    if ( -e $TCLLIBPATH/tk8.3 ) setenv TK_LIBRARY $TCLLIBPATH/tk8.3
    if ( -e $TCLLIBPATH/tk8.4 ) setenv TK_LIBRARY $TCLLIBPATH/tk8.4
    if ( -e $TCLLIBPATH/tk8.5 ) setenv TK_LIBRARY $TCLLIBPATH/tk8.5
    if ( -e $TCLLIBPATH/tix8.1 )   setenv TIX_LIBRARY $TCLLIBPATH/tix8.1
    if ( -e $TCLLIBPATH/Tix8.4.2 ) setenv TIX_LIBRARY $TCLLIBPATH/Tix8.4.2
    if ( -e $TCLLIBPATH/blt2.4 ) setenv BLT_LIBRARY $TCLLIBPATH/blt2.4
    if ( -e $TCLLIBPATH/blt2.5 ) setenv BLT_LIBRARY $TCLLIBPATH/blt2.5
    if(! $?LD_LIBRARY_PATH ) then
      setenv LD_LIBRARY_PATH $TCLLIBPATH
    else
      setenv LD_LIBRARY_PATH "$TCLLIBPATH":"$LD_LIBRARY_PATH"
    endif
    if(! $?DYLD_LIBRARY_PATH ) then
      setenv DYLD_LIBRARY_PATH $TCLLIBPATH
    else
      setenv DYLD_LIBRARY_PATH "$TCLLIBPATH":"$DYLD_LIBRARY_PATH"
    endif
  endif

  # default: assume platform has Tcl/Tk/Tix/BLT libs installed somewhere
endif
