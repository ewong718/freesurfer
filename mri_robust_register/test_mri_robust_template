#!/bin/tcsh -f

#
# test_mri_robust_template
#
# execute mri_robust_template and check results against expected results
#
# Original Author: Nick Schmansky
# CVS Revision Info:
#    $Author: nicks $
#    $Date: 2011/05/02 21:02:42 $
#    $Revision: 1.2 $
#
# Copyright Â© 2011 The General Hospital Corporation (Boston, MA) "MGH"
#
# Terms and conditions for use, reproduction, distribution and contribution
# are found in the 'FreeSurfer Software License Agreement' contained
# in the file 'LICENSE' found in the FreeSurfer distribution, and here:
#
# https://surfer.nmr.mgh.harvard.edu/fswiki/FreeSurferSoftwareLicense
#
# Reporting: freesurfer@nmr.mgh.harvard.edu
#
# General inquiries: freesurfer@nmr.mgh.harvard.edu
#

set VERSION='$Id: test_mri_robust_template,v 1.2 2011/05/02 21:02:42 nicks Exp $'

umask 002

# check for an enviro var to skip
if ( $?SKIP_MRI_ROBUST_TEMPLATE_TEST ) exit 77

#
# extract testing data
#
gunzip -c testdata.tar.gz | tar xvf -

#
# run mri_robust_template, using parameters used for motioncor, using identical
# input images, such that the output is expected to be identical to the inputs
#

setenv SUBJECTS_DIR $PWD
set cmd=(./mri_robust_template)
set cmd = ($cmd --mov 001.mgz 001.mgz)
set cmd = ($cmd --average 1)
set cmd = ($cmd --template rawavg.mgz)
set cmd = ($cmd --satit)
set cmd = ($cmd --inittp 1)
set cmd = ($cmd --fixtp)
set cmd = ($cmd --noit)
set cmd = ($cmd --iscale)
set cmd = ($cmd --subsample 200)
echo ""
echo $cmd
echo $cmd >& test_mri_robust_template.log
echo ""
echo "logging output to test_mri_robust_template.log..."
$cmd >>& test_mri_robust_template.log
if ($status != 0) then
  echo "mri_robust_template FAILED"
  exit 1
endif

#
# compare expected results with actual (produced) results, which should be
# identical (inputs == output)
#
compare:
set MRIDIFFF=(mri_diff.log)
set REF_VOL=(001.mgz)
set TST_VOL=(rawavg.mgz)
set DIFF_VOL=(mri_diff-rawavg.mgz)
set cmd=(../mri_diff/mri_diff \
            --notallow-acq \
            --debug \
            --thresh 0.0 \
            --log $MRIDIFFF \
            $REF_VOL $TST_VOL --diff $DIFF_VOL);
echo ""
echo $cmd
echo $cmd >>& test_mri_robust_template.log
$cmd >>& test_mri_robust_template.log
set diff_status=$status
if ($diff_status != 0) then
  echo "$cmd FAILED (exit status=$diff_status)"
  exit 1
endif

#
# cleanup
#
end:
rm -Rf 001.mgz rawavg.mgz

echo ""
echo "test_mri_robust_template passed all tests"
exit 0
