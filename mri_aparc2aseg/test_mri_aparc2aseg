#!/bin/tcsh -f

#
# test_mri_aparc2aseg
#
# execute mri_aparc2aseg and check results against expected results
#
# Original Author: Nick Schmansky
# CVS Revision Info:
#    $Author: nicks $
#    $Date: 2011/05/02 21:02:41 $
#    $Revision: 1.10 $
#
# Copyright Â© 2011 The General Hospital Corporation (Boston, MA) "MGH"
#
# Terms and conditions for use, reproduction, distribution and contribution
# are found in the 'FreeSurfer Software License Agreement' contained
# in the file 'LICENSE' found in the FreeSurfer distribution, and here:
#
# https://surfer.nmr.mgh.harvard.edu/fswiki/FreeSurferSoftwareLicense
#
# Reporting: freesurfer@nmr.mgh.harvard.edu
#
# General inquiries: freesurfer@nmr.mgh.harvard.edu
# Bug reports: analysis-bugs@nmr.mgh.harvard.edu
#

set VERSION='$Id: test_mri_aparc2aseg,v 1.10 2011/05/02 21:02:41 nicks Exp $'

umask 002

# backdoor bypass:
if ( $?SKIP_MRI_APARC2ASEG_TEST ) then
  echo "skipping test_mri_aparc2aseg"
  exit 77
endif

setenv SUBJECTS_DIR $PWD

if ( -e testdata) then
  echo "rm -Rf testdata"
  rm -Rf testdata
endif

echo "gunzip -c testdata.tar.gz | tar xvf -"
gunzip -c testdata.tar.gz | tar xvf -

set cmd=(./mri_aparc2aseg --s testdata)
echo ""
echo $cmd
echo "Output is directed to file 'test_mri_aparc2aseg.log'..."
$cmd >& test_mri_aparc2aseg.log
if ($status != 0) then
  echo "test_mri_aparc2aseg FAILED"
  exit 1
endif

set TEST_FILES=( aparc+aseg.mgz )
foreach tstvol ($TEST_FILES)
  # now compare expected with observed data
  set REF_VOL  = testdata/mri/reference_aparc+aseg.mgz
  set TST_VOL  = testdata/mri/$tstvol
  set DIFF_VOL = testdata/mri/mri_diff-$tstvol
  set MRIDIFFF = testdata/mri/mri_diff-$tstvol.txt
  set cmd=(../mri_diff/mri_diff --debug \
            --thresh 0.0 \
            --log $MRIDIFFF \
            $REF_VOL $TST_VOL --diff $DIFF_VOL);
  echo ""
  echo $cmd
  $cmd
  set diff_status=$status
  if ($diff_status != 0) then
    echo "test_aparc2aseg $cmd FAILED (exit status=$diff_status)"
    exit 1
  endif
end

####################################################################
# this test is known to fail on when mri_aparc2aseg is built against
# utils/mrishash.c v1.30-v1.33, it exits mri_aparc2aseg with ERROR msg
#
test2:
if ( -e testdata2) then
  echo "rm -Rf testdata2"
  rm -Rf testdata2
endif

echo "gunzip -c testdata2.tar.gz | tar xvf -"
gunzip -c testdata2.tar.gz | tar xvf -

set cmd=( ./mri_aparc2aseg --s testdata2 \
            --labelwm \
            --hypo-as-wm \
            --rip-unknown \
            --o $SUBJECTS_DIR/testdata2/mri/wmparc.mgz )
echo ""
echo $cmd
echo "Output is directed to file 'test_mri_aparc2aseg.log'..."
$cmd >>& test_mri_aparc2aseg.log
if ($status != 0) then
  echo "test_mri_aparc2aseg FAILED"
  exit 1
endif

test2diff:
# now compare expected with observed data
set REF_VOL  = testdata2/mri/reference_wmparc.mgz
set TST_VOL  = testdata2/mri/wmparc.mgz
set DIFF_VOL = testdata2/mri/mri_diff-wmparc.mgz
set MRIDIFFF = testdata2/mri/mri_diff-wmparc.mgz.txt
set cmd=(../mri_diff/mri_diff --debug \
          --thresh 0.0 \
          --log $MRIDIFFF \
          $REF_VOL $TST_VOL --diff $DIFF_VOL);
echo ""
echo $cmd
$cmd
set diff_status=$status
if ($diff_status != 0) then
  echo "test_aparc2aseg $cmd FAILED (exit status=$diff_status)"
  exit 1
endif

#
# cleanup
#
cd $SUBJECTS_DIR
rm -Rf testdata testdata2 

echo ""
echo "test_mri_aparc2aseg passed all tests"
exit 0
